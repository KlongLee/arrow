# Licensed to the Apache Software Foundation (ASF) under one
# or more contributor license agreements.  See the NOTICE file
# distributed with this work for additional information
# regarding copyright ownership.  The ASF licenses this file
# to you under the Apache License, Version 2.0 (the
# "License"); you may not use this file except in compliance
# with the License.  You may obtain a copy of the License at
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing,
# software distributed under the License is distributed on an
# "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
# KIND, either express or implied.  See the License for the
# specific language governing permissions and limitations
# under the License.
#
# This config sets the following variables in your project::
#
#   PLASMA_STORE_SERVER - path to the found plasma-store-server
#   Plasma_FOUND - true if Plasma found on the system
#
# This config sets the following targets in your project::
#
#   Plasma::plasma_shared - for linked as shared library if shared library is built
#   Plasma::plasma_static - for linked as static library if static library is built
#   Plasma::plasma-store-server - for plasma-store-server executable

@PACKAGE_INIT@

include(CMakeFindDependencyMacro)
find_dependency(Arrow)
if(ARROW_CUDA)
  find_dependency(ArrowCUDA)
endif()

set(PLASMA_STORE_SERVER "@CMAKE_INSTALL_FULL_BINDIR@/plasma-store-server@CMAKE_EXECUTABLE_SUFFIX@")

include("${CMAKE_CURRENT_LIST_DIR}/PlasmaTargets.cmake")

# For backward compatibility
if(NOT CMAKE_VERSION VERSION_LESS 3.18)
  if(TARGET Plasma::plasma_shared AND NOT TARGET plasma_shared)
    add_library(plasma_shared ALIAS Plasma::plasma_shared)
  endif()
  if(TARGET Plasma::plasma_static AND NOT TARGET plasma_static)
    add_library(plasma_static ALIAS Plasma::plasma_static)
  endif()
endif()

check_required_components(Plasma)
